plugins {
    id 'java'
    id 'cpp'
}

group 'jni.example'
version '1.0.0'

sourceCompatibility = 1.8

sourceSets {
    main {
        resources {
            srcDir "${buildDir}/libs/example/shared"
        }
    }
}

model {
    components {
        example(NativeLibrarySpec) {
            sources.cpp { 
                exportedHeaders.srcDir "${buildDir}/generated/include"
            }
            binaries.all {
                cppCompiler.args "-I/usr/include"
                cppCompiler.args "-I/usr/local/include"
                cppCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                cppCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                cppCompiler.args "-std=c++11"
                linker.args "-lstdc++"
            }
        }
    }
}

repositories {
    mavenCentral()
}

test {
    useTestNG()
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
}

task javahExample(type:Exec) {
    String javaSrcDir = "${projectDir}/src/main/java"
    String cppSrcDir = "${buildDir}/generated/include"
    inputs.file "${javaSrcDir}/jni/example/Example.java"
    outputs.dir cppSrcDir
    workingDir cppSrcDir
    commandLine 'javah', '-cp', javaSrcDir, 'jni.example.Example'
}

tasks.getByPath(':exampleSharedLibrary').dependsOn javahExample

processResources.dependsOn { exampleSharedLibrary }