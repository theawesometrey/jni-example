plugins {
    id 'java'
    id 'cpp'
}

group 'jni.example'
version '1.0.0'

sourceCompatibility = 1.8

model {
    components {
        example(NativeLibrarySpec) {
            binaries.all {
                cppCompiler.args "-I/usr/include"
                cppCompiler.args "-I/usr/local/include"
                cppCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                cppCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                cppCompiler.args "-std=c++11"
                linker.args "-lstdc++"
            }
        }
    }
}

repositories {
    mavenCentral()
}

test {
    useTestNG()
    systemProperty "java.library.path", "$buildDir/libs/example/shared"
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
}

task javahExample(type:Exec) {
    String javaSrcDir = "${projectDir}/src/main/java"
    String cppSrcDir = "${projectDir}/src/example/cpp"
    inputs.file "${javaSrcDir}/jni/example/Example.java"
    outputs.dir "$cppSrcDir"
    workingDir "$cppSrcDir"
    commandLine 'javah', '-cp', "$javaSrcDir", 'jni.example.Example'
}

tasks.getByPath(':exampleSharedLibrary').dependsOn javahExample

test.dependsOn { exampleSharedLibrary }
