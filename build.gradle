plugins {
    id 'java'
    id 'cpp'
}

group 'jni.example'
version '1.0.0'

sourceCompatibility = 1.8

model {
    components {
        example(NativeLibrarySpec)
    }
    toolChains {
        gcc(Gcc) {
            eachPlatform {
                cppCompiler.withArguments { args ->
                    args << "-I/usr/include"
                    args << "-I/usr/local/include"
                    args << "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    args << "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                    args << "-std=c++11"
                }
                linker.withArguments { args ->
                    args << "-lstdc++"
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

test {
    useTestNG()
    systemProperty "java.library.path", "$buildDir/libs/example/shared"
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
}

task javah(type:Exec) {
    workingDir "src/example/cpp"
    commandLine 'javah', '-cp', file("$projectDir/src/main/java").getAbsolutePath(), 'jni.example.Example'
}

tasks.getByPath(':exampleSharedLibrary').dependsOn javah

test.dependsOn { exampleSharedLibrary }
